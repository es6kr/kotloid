buildscript {
	ext.kotlin_version = project.KOTLIN_VERSION

	repositories {
		jcenter()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "com.github.dcendents:android-maven-plugin:1.2"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
	}
}

apply plugin: 'maven'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
	compileSdkVersion 25
	buildToolsVersion '26.0.1'

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion 25
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
	}
}

apply plugin: 'kotlin-android'

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	destinationDir = file("../javadoc/")
	failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

archivesBaseName = 'kotloid'
group 'kr.or.lightsalt'
version project.KOTLIB_VERSION

repositories {
	maven { url "https://jitpack.io" }
}

dependencies {
	compile "$group:kotlib:$version"
	compile "com.android.support:design:$project.ANDROID_SUPPORT_VERSION"
	compile 'com.github.delight-im:Android-AdvancedWebView:v3.0.0'
	compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
	testCompile "junit:junit:4.12"
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$project.KOTLIN_VERSION"
}

Properties properties = new Properties()
try {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Exception ignore) {
}

bintray {
	user = properties.getProperty('user')
	key = properties.getProperty('apiKey')
	configurations = ['archives']
	override = true
	pkg {
		repo = 'kotlin'
		name = archivesBaseName
		userOrg = properties.getProperty('org')
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/light-and-salt/kotloid.git'
		publish = true
	}
}

install {
	repositories.mavenInstaller {
		pom {
			project {
				packaging 'aar'
			}
		}
	}
}
