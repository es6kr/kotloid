buildscript {
	ext {
		kotlin_ver = project.findProperty('KOTLIN_VERSION') ?: '+'
		support_ver = project.findProperty('ANDROID_SUPPORT_VERSION') ?: '+'
	}

	repositories {
		google()
		jcenter()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_ver"
		classpath "com.github.dcendents:android-maven-gradle-plugin:2.0"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
	}
}

apply plugin: 'maven'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
	compileSdkVersion 27
	buildToolsVersion '27.0.3'
	useLibrary 'org.apache.http.legacy'

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 27
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
	}
}

apply plugin: 'kotlin-android'

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	destinationDir = file("../javadoc/")
	failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

archivesBaseName = 'kotloid'
group 'kr.or.lightsalt'
version project.findProperty('KOTLIB_VERSION')

repositories {
	google()
	jcenter()
	maven { url 'https://jitpack.io' }
}

dependencies {
	try {
		api project(':kotlib')
	} catch (UnknownProjectException ignored) {
		api 'com.github.light-and-salt:kotlib:-SNAPSHOT'
	}
	api "com.android.support:design:$support_ver"
	api 'com.github.delight-im:Android-AdvancedWebView:v3.0.0'
	api "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_ver"
	testApi "junit:junit:4.12"
	testApi "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_ver"
}

Properties properties = new Properties()
try {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Exception ignore) {
}

bintray {
	user = properties.getProperty('user')
	key = properties.getProperty('apiKey')
	configurations = ['archives']
	override = true
	pkg {
		repo = 'kotlin'
		name = archivesBaseName
		userOrg = properties.getProperty('org')
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/light-and-salt/kotloid.git'
		publish = true
	}
}

install {
	repositories.mavenInstaller {
		pom {
			project {
				packaging 'aar'
			}
		}
	}
}
